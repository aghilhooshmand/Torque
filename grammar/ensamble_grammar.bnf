<program> ::= <ensemble>

<ensemble> ::= "vote" "(" <models> ";" <ensemble_params> ")"
             | "vote" "(" <models> ")"
             | "stack" "(" <models> ";" <ensemble_params> ")"
             | "stack" "(" <models> ")"
             | "bag" "(" <model> ";" <ensemble_params> ")"
             | "bag" "(" <model> ")"
             | "ada" "(" <model> ";" <ensemble_params> ")"
             | "ada" "(" <model> ")"

<models> ::= <model>
           | <model> "," <model>
           | <model> "," <model> "," <model>

<model> ::= "LR" "(" <lr_params> ")"
          | "DT" "(" <dt_params> ")"
          | "NB" "(" <nb_params> ")"
          | "SVM" "(" <svm_params> ")"
          | "KNN" "(" <knn_params> ")"
          | "RF" "(" <rf_params> ")"
          | "GB" "(" <gb_params> ")"
          | "XGB" "(" <xgb_params> ")"

<lr_params> ::= "C" "=" <positive_number>
              | "penalty" "=" <penalty_string>
              | "C" "=" <positive_number> "," "max_iter" "=" <int_number>

<dt_params> ::= "max_depth" "=" <int_number>
             | "max_depth" "=" <int_number> "," "criterion" "=" <criterion_string>

<nb_params> ::= "var_smoothing" "=" <small_number>

<svm_params> ::= "C" "=" <positive_number>
               | "kernel" "=" <kernel_string>
               | "C" "=" <positive_number> "," "kernel" "=" <kernel_string>

<knn_params> ::= "n_neighbors" "=" <int_number>

<rf_params> ::= "n_estimators" "=" <int_number>
              | "n_estimators" "=" <int_number> "," "max_depth" "=" <int_number>

<gb_params> ::= "n_estimators" "=" <int_number>
              | "n_estimators" "=" <int_number> "," "learning_rate" "=" <positive_number>

<xgb_params> ::= "n_estimators" "=" <int_number>
               | "n_estimators" "=" <int_number> "," "max_depth" "=" <int_number>

<penalty_string> ::= "l1" | "l2" | "elasticnet"

<kernel_string> ::= "linear" | "rbf"

<criterion_string> ::= "gini" | "entropy" | "log_loss"

<positive_number> ::= "0.1" | "0.5" | "1.0" | "10" | "50"

<small_number> ::= "1e-9" | "1e-8" | "1e-7" | "1e-6"

<ensemble_params> ::= <voting_param>
                    | <final_estimator_param>
                    | <n_estimators_param>

<voting_param> ::= "voting" "=" <voting_string>

<voting_string> ::= "hard" | "soft"

<final_estimator_param> ::= "final_estimator" "=" <model_name>

<n_estimators_param> ::= "n_estimators" "=" <int_number>

<model_name> ::= "LR" | "DT" | "NB" | "SVM" | "KNN" | "RF" | "GB" | "XGB"

<int_number> ::= "1" | "2" | "3" | "5" | "10" | "20" | "50"

