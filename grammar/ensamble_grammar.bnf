<program> ::= <ensemble>

<ensemble> ::= "vote" "(" <models> ";" <vote_params> ")"
             | "vote" "(" <models> ")"
             | "stack" "(" <models> ";" <stack_params> ")"
             | "stack" "(" <models> ")"
             | "bag" "(" <model> ";" <bag_params> ")"
             | "bag" "(" <model> ")"
             | "ada" "(" <model> ";" <ada_params> ")"
             | "ada" "(" <model> ")"

<models> ::= <model>
           | <model> "," <models>

<model> ::= "LR" "(" <lr_params> ")"
          | "DT" "(" <dt_params> ")"
          | "NB" "(" <nb_params> ")"
          | "SVM" "(" <svm_params> ")"
          | "KNN" "(" <knn_params> ")"
          | "RF" "(" <rf_params> ")"
          | "GB" "(" <gb_params> ")"
          | "XGB" "(" <xgb_params> ")"

<lr_params> ::= "C" "=" <positive_number>
              | "penalty" "=" <penalty_string>
              | "C" "=" <positive_number> "," "max_iter" "=" <int_number>

<dt_params> ::= "max_depth" "=" <int_number>
             | "criterion" "=" <criterion_string>
             | "max_depth" "=" <int_number> "," "criterion" "=" <criterion_string>
             | "max_depth" "=" <int_number> "," "min_samples_split" "=" <min_samples_value>
             | "max_depth" "=" <int_number> "," "criterion" "=" <criterion_string> "," "min_samples_split" "=" <min_samples_value>
             | "max_depth" "=" <int_number> "," "min_samples_leaf" "=" <min_samples_leaf_value>

<nb_params> ::= "var_smoothing" "=" <small_number>

<svm_params> ::= "C" "=" <positive_number>
               | "kernel" "=" <kernel_string>
               | "C" "=" <positive_number> "," "kernel" "=" <kernel_string>

<knn_params> ::= "n_neighbors" "=" <int_number>

<rf_params> ::= "n_estimators" "=" <int_number>
              | "max_depth" "=" <int_number>
              | "n_estimators" "=" <int_number> "," "max_depth" "=" <int_number>

<gb_params> ::= "learning_rate" "=" <positive_number>
              | "n_estimators" "=" <int_number>
              | "n_estimators" "=" <int_number> "," "learning_rate" "=" <positive_number>

<xgb_params> ::= "n_estimators" "=" <int_number>
               | "max_depth" "=" <int_number>
               | "n_estimators" "=" <int_number> "," "max_depth" "=" <int_number>

<penalty_string> ::= "l1" | "l2" | "elasticnet"

<kernel_string> ::= "linear" | "rbf" | "poly" | "sigmoid"

<criterion_string> ::= "gini" | "entropy" | "log_loss"

<min_samples_value> ::= <int_number> | <min_samples_ratio>

<min_samples_ratio> ::= "0.1" | "0.2" | "0.3" | "0.4" 

<min_samples_leaf_value> ::= <int_number> | <min_samples_ratio>

<positive_number> ::= "0.1" | "0.5" | "1.0" | "10" | "50"

<small_number> ::= "1e-9" | "1e-8" | "1e-7" | "1e-6"

<vote_params> ::= "voting" "=" <voting_string>

<voting_string> ::= "hard" | "soft"

<stack_params> ::= "final_estimator" "=" <model_name>
                 | "cv" "=" <int_number>
                 | "final_estimator" "=" <model_name> "," "cv" "=" <int_number>

<bag_params> ::= "n_estimators" "=" <int_number>
               | "max_samples" "=" <positive_number>
               | "max_features" "=" <positive_number>
               | "n_estimators" "=" <int_number> "," "max_samples" "=" <positive_number>
               | "n_estimators" "=" <int_number> "," "max_features" "=" <positive_number>

<ada_params> ::= "n_estimators" "=" <int_number>
               | "learning_rate" "=" <positive_number>
               | "algorithm" "=" <ada_algorithm_string>
               | "n_estimators" "=" <int_number> "," "learning_rate" "=" <positive_number>
               | "n_estimators" "=" <int_number> "," "algorithm" "=" <ada_algorithm_string>

<ada_algorithm_string> ::= "SAMME" | "SAMME.R"

<model_name> ::= "LR" | "DT" | "NB" | "SVM" | "KNN" | "RF" | "GB" | "XGB"

<int_number> ::= "1" | "2" | "3" | "5" | "10" | "50" 
